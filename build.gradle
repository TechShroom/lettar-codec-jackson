buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.1.0'
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.14.0"
 	id "net.researchgate.release" version "2.6.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'aversion-util'
apply plugin: 'aversion-apt'
apply plugin: 'aversion-maven'

license {
    ext {
        name = rootProject.name
        organization = rootProject.organization
        url = rootProject.url
    }
    header = rootProject.file('HEADER.txt')
    ignoreFailures = false
    strictCheck = true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

util {
    javaVersion = '1.8'
}

mavencfg {
    coords 'TechShroom', 'lettar-codec-jackson'
    description 'Lettar Codec - Jackson'
}

configurations.all {
   resolutionStrategy {
     // fail eagerly on version conflict (includes transitive dependencies)
     // e.g. multiple different versions of the same dependency (group and name are equal)
     failOnVersionConflict()

     // force certain versions of dependencies (including transitive)
     force 'com.google.code.findbugs:jsr305:3.0.1'
     force 'com.fasterxml.jackson.core:jackson-annotations:' + jacksonVersion
     force 'com.google.guava:guava:24.1-jre'

     cacheDynamicVersionsFor 10, 'minutes'
     // don't cache changing modules at all
     cacheChangingModulesFor 0, 'seconds'
   }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    compileOnly group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'
    compile group: 'com.techshroom', name: 'lettar', version: '0.3.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: project.jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: project.jacksonVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.fork = true
}
